#import LetterDelivery.mpat
#import nok_03_Fuzzy.mpat
#import trd_01_Bobomb.mpat
#import nok_14_ParaTroopa.mpat

#define .NpcID:NpcGuard 1`
#define .NpcID:NpcFuzzy1 2`
#define .NpcID:NpcFuzzy2 3`
#define .NpcID:NpcBobomb1 4`
#define .NpcID:NpcKoopa1 5`
#define .NpcID:DiamondHelper 6`

#new:Function $Function_PlaySpringAnimation
{
    0:  ADDIU     SP, SP, FFE8
    4:  SW        RA, 10 (SP)
    8:  LW        V0, C (A0)
    C:  JAL       ~Func:get_variable
   10:  LW        A1, 0 (V0)
   14:  JAL       ~Func:get_entity_by_index
   18:  COPY      A0, V0
   1C:  BEQL      V0, R0, .o3C
   20:  CLEAR     V0
   24:  LH        A0, 14 (V0)
   28:  LA        A1, 000001E4
   30:  JAL       ~Func:play_model_animation
   34:  NOP
   38:  LI        V0, 2
        .o3C
   3C:  LW        RA, 10 (SP)
   40:  JR        RA
   44:  ADDIU     SP, SP, 18
}

#new:Function $Function_SetEntityPosition
{
    0:  ADDIU     SP, SP, FFD8
    4:  SW        S1, 14 (SP)
    8:  COPY      S1, A0
    C:  SW        RA, 20 (SP)
   10:  SW        S3, 1C (SP)
   14:  SW        S2, 18 (SP)
   18:  SW        S0, 10 (SP)
   1C:  LW        S0, C (S1)
   20:  LW        A1, 0 (S0)
   24:  JAL       ~Func:get_variable
   28:  ADDIU     S0, S0, 4
   2C:  LW        A1, 0 (S0)
   30:  ADDIU     S0, S0, 4
   34:  COPY      A0, S1
   38:  JAL       ~Func:get_variable
   3C:  COPY      S3, V0
   40:  LW        A1, 0 (S0)
   44:  ADDIU     S0, S0, 4
   48:  COPY      A0, S1
   4C:  JAL       ~Func:get_variable
   50:  COPY      S2, V0
   54:  COPY      A0, S1
   58:  LW        A1, 0 (S0)
   5C:  JAL       ~Func:get_variable
   60:  COPY      S1, V0
   64:  COPY      A0, S3
   68:  JAL       ~Func:get_entity_by_index
   6C:  COPY      S0, V0
   70:  COPY      V1, V0
   74:  MTC1      S2, F0
   78:  NOP
   7C:  CVT.S.W   F0, F0
   80:  SWC1      F0, 48 (V1)
   84:  MTC1      S1, F0
   88:  NOP
   8C:  CVT.S.W   F0, F0
   90:  SWC1      F0, 4C (V1)
   94:  MTC1      S0, F0
   98:  NOP
   9C:  CVT.S.W   F0, F0
   A0:  SWC1      F0, 50 (V1)
   A4:  LW        RA, 20 (SP)
   A8:  LW        S3, 1C (SP)
   AC:  LW        S2, 18 (SP)
   B0:  LW        S1, 14 (SP)
   B4:  LW        S0, 10 (SP)
   B8:  LI        V0, 2
   BC:  JR        RA
   C0:  ADDIU     SP, SP, 28
}

#new:Script $Script_OpenChestAnimation
{
    0:  SetGroup  00000000
    C:  Call  SetTimeFreezeMode ( 00000002 )
   1C:  Wait  40`
   28:  Call  ShowGotItem       ( *Var0 .False 00000000 )
   40:  Call  SetTimeFreezeMode ( 00000000 )
   50:  Return
   58:  Return
   60:  End
}

#new:Script $Script_OpenChest
{
    0:  Call  DisablePlayerInput    ( .True )
   10:  Set   *Var0  *VarA
   20:  If  *VarA  !=  00000000
   30:  	ExecWait  $Script_OpenChestAnimation
   3C:  EndIf
   44:  Switch  *VarB
   50:  	Case  ==  00000000
   5C:  		Call  AddItem   ( *VarA *Var0 )
   70:  	Case  ==  00000001
   7C:  		Call  AddKeyItem    ( *VarA )
   8C:  	Case  ==  00000002
   98:  		Call  AddBadge  ( *VarA *Var0 )
   AC:  EndSwitch
   B4:  Wait  15`
   C0:  Call  DisablePlayerInput    ( .False )
   D0:  Return
   D8:  End
}

@ $Script_Main {
	Set   *GB_WorldLocation  .Location:GoombaRoad
	Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
	Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
	Call  SetCamEnabled      ( .Cam:Default .True
	Call  SetCamLeadPlayer   ( .Cam:Default .False )
	
	% replaced with Main, UpperPath and RightPath scripts
	% Exec  $Script_MakeEntities
	Exec  $Script_SetupMusic
	Exec  $Script_SetupTexturePan
	Exec  $Script_EnterMap
	
	% Custom
	Exec  $Script_MakeMainEntities
	
	If *MF_btf_06_PlatformsActivated == .True
		Exec  $Script_SetupPlatforms
	EndIf
	
	If *MF_btf_06_DoorActivated == .True
		Exec $Script_DisableDoor
	Else
		If *MF_btf_06_CollectedDiamondStone == .True
			Call MakeItemEntity ( .Item:DiamondStone ~Vec3D:DiamondEmblemPosition .ItemSpawnMode:Decoration 0 )
			Set *MapVar[5] *Var0
		EndIf
		If *MF_btf_06_CollectedLunarStone == .True
			Call MakeItemEntity ( .Item:LunarStone ~Vec3D:LunarEmblemPosition .ItemSpawnMode:Decoration 0 )
			Set *MapVar[6] *Var0
		EndIf
	EndIf
	
	If *MF_btf_06_ClubbaGone == .True
		Call ModifyColliderFlags ( 0 ~Collider:switch_room_blocker 00018000 )
	EndIf
	
	Call MakeNpcs ( .False $NpcGroupList )
	Call SetNpcYaw ( .NpcID:NpcGuard -127` ) % Makes Clubba face left
	Bind $Script_Platform_Fall .Trigger:FloorTouch ~Collider:platform1 00000001 00000000
	Bind $Script_ResetMario1 .Trigger:CeilingTouch ~Collider:reset_collider_1 00000001 00000000
	Bind $Script_ResetMario1 .Trigger:CeilingTouch ~Collider:reset_collider_2 00000001 00000000
	Bind $Script_ResetMario1 .Trigger:CeilingTouch ~Collider:reset_collider_3 00000001 00000000
	
	Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:fuzzy_platform_floor 00000001 00000000
	Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:right_path_floor 00000001 00000000
	Bind $Script_SetResetPos_SignPlatform .Trigger:FloorTouch ~Collider:sign_platform_floor 00000001 00000000
	
	% Manually initialize npcs and entities
	Thread
		Call AwaitPlayerApproach ( ~Vec2D:Bobomb1ApproachPos 160` )
		Exec $Script_ManuallyInit_NpcBobomb1
		Exec $Script_MakeRightPathEntities
	EndThread
	
	Thread
		Loop
			Call GetPlayerPos ( *VarA *VarB *VarC )
			
			If *VarB > 128`
				BreakLoop
			EndIf
			
			Wait 1`
		EndLoop
		
		Exec $Script_ManuallyInit_NpcKoopa1
		Exec $Script_MakeUpperPathEntities
		
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor0 00000001 00000000
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor1 00000001 00000000
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor2 00000001 00000000
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor3 00000001 00000000
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor4 00000001 00000000
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor5 00000001 00000000
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor6 00000001 00000000
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor7 00000001 00000000
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor8 00000001 00000000
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor9 00000001 00000000
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor10 00000001 00000000
		Bind $Script_SetResetPos_Dynamic .Trigger:FloorTouch ~Collider:upper_path_floor11 00000001 00000000
	EndThread
	
	Return
	End
}

@ $Script_SetupMusic {
	Call  SetMusicTrack ( 00000000 .Song:SpaceJunk 00000000 00000008 )
	Call  ClearAmbientSounds ( 250` )
	Return
	End
}

@Message $String_ReadSign_Signpost1
{
	[DelayOff][STYLE:SIGN]
	[CenterX:FF]
	[Down:03]Only the quickest heroes may[BR]dare to ride the platforms.
	[DelayOn][WAIT][END]
}

% Custom entities
#new:Script $Script_MakeMainEntities {
	Call MakeItemEntity ( .Item:YellowBerry ~Vec3d:Item1 .ItemSpawnMode:Fixed_NeverVanish *MF_btf_06_CollectedItem1 )
	Call MakeItemEntity ( .Item:SlowGo ~Vec3d:StairSecret .ItemSpawnMode:Fixed_NeverVanish *MF_btf_06_FinalSendoffFromB )
	
	Call MakeEntity     ( .Entity:Signpost ~Vec4d:Signpost1 80000000 )
	Call AssignScript   ( $Script_ReadSign_Signpost1 )
	
	Call MakeEntity     ( .Entity:YellowBlock ~Vec4d:SaveBlock .Item:Coin 80000000 )
	Call AssignBlockFlag    ( *MF_btf_06_HitCoinBlock )
	
	If *MF_btf_06_SpringFallen == .True
		Call  MakeEntity ( .Entity:ScriptSpring ~Vec4d:SpringEndPosition 80000000 )
		Call  AssignScript ( $Script_ScriptedSpring_Spring )
	Else
		Call  MakeEntity ( .Entity:ScriptSpring ~Vec4d:Spring 80000000 )
		Call  AssignScript ( $Script_ScriptedSpring_Spring )
		% id of spring entity
		Set   *MapVar[4] *Var0
	EndIf
	
	Return
	End
}

#new:Script $Script_MakeRightPathEntities {
	Call MakeItemEntity ( .Item:DiamondStone ~Vec3d:ItemDiamondStone .ItemSpawnMode:Fixed_NeverVanish *MF_btf_06_CollectedDiamondStone )
	
	Call MakeEntity     ( .Entity:Chest ~Vec4d:Chest1 00000000 80000000 )
	Call AssignFlag     ( *MF_btf_06_OpenedChest1 )
	Call AssignScript   ( $Script_UseChest1 )
	
	Return
	End
}

#new:Script $Script_MakeUpperPathEntities {
	Call MakeItemEntity ( .Item:LunarStone ~Vec3d:ItemLunarStone .ItemSpawnMode:Fixed_NeverVanish *MF_btf_06_CollectedLunarStone )
	
	Call MakeEntity     ( .Entity:MultiCoinBrick ~Vec4d:BrickBlock1 80000000 )
	
	Call MakeEntity     ( .Entity:Chest ~Vec4d:Chest2 00000000 80000000 )
	Call AssignFlag     ( *MF_btf_06_OpenedChest2 )
	Call AssignScript   ( $Script_UseChest2 )
	
	Call MakeEntity     ( .Entity:YellowBlock ~Vec4d:ItemBlock1 .Item:DizzyDial 80000000 )
	Call AssignBlockFlag    ( *MF_btf_06_HitItemBlock1 )
	
	Call MakeEntity     ( .Entity:ScriptSpring ~Vec4d:Spring2 80000000 )
	Call AssignScript   ( $Script_ScriptedSpring_Spring2 )
	Call MakeEntity     ( .Entity:ScriptSpring ~Vec4d:Spring3 80000000 )
	Call AssignScript   ( $Script_ScriptedSpring_Spring3 )
	Call MakeEntity     ( .Entity:ScriptSpring ~Vec4d:Spring4 80000000 )
	Call AssignScript   ( $Script_ScriptedSpring_Spring4 )
	
	Return
	End
}

#new:Script $Script_UseChest1
{
	Set *VarA .Item:PUpDDown
	Set *VarB 00000002 % 2 for badge
	Set *MF_btf_06_OpenedChest1 .True
	ExecWait $Script_OpenChest
	Return
	End
}

#new:Script $Script_UseChest2
{
	Set *VarA .Item:AttackFXF
	Set *VarB 00000002 % 2 for badge
	Set *MF_btf_06_OpenedChest2 .True
	ExecWait $Script_OpenChest
	Return
	End
}

#new:Script $Script_ScriptedSpring_Spring
{
	Call  DisablePlayerInput    ( .True )
	Call  DisablePlayerPhysics  ( .True )
	Call  SetPlayerActionState  ( .ActionState:Launch )
	Wait  1`
	Exec  $Script_ScriptedSpring_FollowCam *Var[A]
	Call  SetPlayerJumpscale    ( *Fixed[0.7] )
	Call  PlayerJump            ( ~Vec3d:Spring_Destination 30` )
	Kill  *Var[A]
	Call  SetPlayerActionState  ( .ActionState:Idle )
	Call  DisablePlayerPhysics  ( .False )
	Call  DisablePlayerInput    ( .False )
	Return
	End
}

% Cutscenes
#new:Script $Script_DisableDoor {
	Set *MF_btf_06_DoorActivated .True
	
	Call EnableModel ( ~Model:DoorBBlocker1 .False )
	Call EnableModel ( ~Model:DoorBBlocker2 .False )
	
	Call ModifyColliderFlags ( 0 ~Collider:LetterDoor 00018000 )
	
	Return
	End
}

#new:Script $Script_ShowDiamondCutscene {
	Call DisablePlayerInput ( .True )
	
	Wait 10`
	
	Call GetPlayerPos ( *VarA *VarB *VarC )
	Call ShowEmote ( 00000000 .Emote:Question -45` 30` .False *VarA *VarB *VarC 0` )
	
	Wait 35`
	
	ExecWait $Script_PanCamera_DoorCam
	
	Wait 80`
	
	Thread
		Call PlaySound ( 000000B2 )
		Call PlayEffect ( ~FX:LensFlare:Rotating ~Vec3D:DiamondEmblemFX 20` 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )  
	EndThread

	Wait 1`
	
	Call MakeItemEntity ( .Item:DiamondStone ~Vec3D:DiamondEmblemPosition .ItemSpawnMode:Decoration 0 )
	Set *MapVar[5] *Var0
	
	Wait 100`
	
	ExecWait $Script_PanCamera_SpringCam
	
	Wait 15`
	
	Call MakeLerp ( 200` 40` 18` .Easing:QuadraticIn )
	Loop
		Call UpdateLerp ( )
		Call $Function_SetEntityPosition ( *MapVar[4] 638` *Var0 -399` )
		Wait 1`
		If *Var1 == 00000000
			BreakLoop
		EndIf
	EndLoop
	
	Set *MF_btf_06_SpringFallen .True
	
	% SOUND_OBJECT_LAND
	Call PlaySoundAt ( 00000048 00000000 638` *Var0 -399` )
	% SOUND_SPRING
	Call PlaySoundAt ( 00002086 00000000 638` *Var0 -399` )
	Call $Function_PlaySpringAnimation ( *MapVar[4] )
	
	Wait 80`
	
	Call ResetCam ( .Cam:Default *Fixed[4.0] )
	
	Wait 10`
	
	Call DisablePartnerAI ( .False ) % 0 lets me change the partner's animation, 1 does not
	
	% talkAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 05
	% idleAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 02
	% stringID - AAAABBBB - space jam strings resource (0x2F), string 0x28
	Call SpeakToPlayer ( .Npc:Partner 00040006 00040001 00000000 002F003D )
	
	Call EnablePartnerAI ()
	Call DisablePlayerInput ( .False )
	
	Return
	End
}

#new:Script $Script_ShowLunarCutscene {
	Call DisablePlayerInput ( .True )
	
	Wait 10`
	
	Call GetPlayerPos ( *VarA *VarB *VarC )
	Call ShowEmote ( 00000000 .Emote:Question -45` 30` .False *VarA *VarB *VarC 0` )
	
	Wait 35`
	
	ExecWait $Script_PanCamera_DoorCam
	
	Wait 80`
	
	Thread
		Call PlaySound ( 000000B2 )
		Call PlayEffect ( ~FX:LensFlare:Rotating ~Vec3D:LunarEmblemFX 20` 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )  
	EndThread
	
	Wait 1`
	
	Call MakeItemEntity ( .Item:LunarStone ~Vec3D:LunarEmblemPosition .ItemSpawnMode:Decoration 0 )
	Set *MapVar[6] *Var0
	
	Wait 100`
	
	Call PlaySound ( 0000024A )
	Call PlayEffect ( ~FX:GatherEnergyPink ~Vec3D:DoorFX 5` 60` 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )  
	
	Wait 60`
	
	Thread
		Call PlaySound ( 000000B2 )
		Call PlayEffect ( ~FX:LensFlare:Rotating ~Vec3D:DiamondEmblemFX 20` 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )  
	EndThread
	Thread
		Call PlayEffect ( ~FX:LensFlare:Rotating ~Vec3D:LunarEmblemFX 20` 00000000 00000000 00000000 00000000 00000000 00000000 00000000 00000000 )  
	EndThread
	
	Wait 1`
	
	Call RemoveItemEntity ( *MapVar[5] )
	Call RemoveItemEntity ( *MapVar[6] )
	Exec $Script_DisableDoor
	
	Wait 100`
	
	Call ResetCam ( .Cam:Default *Fixed[4.0] )
	
	Wait 10`
	
	Call DisablePartnerAI ( .False )
	
	% talkAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 05
	% idleAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 02
	% stringID - AAAABBBB - space jam strings resource (0x2F), string 0x28
	Call SpeakToPlayer ( .Npc:Partner 00040006 00040001 00000000 002F0050 )
	
	Call EnablePartnerAI ()
	Call DisablePlayerInput ( .False )
	
	Return
	End
}

% Pit Respawning
#new:Script $Script_PanCamera_Dynamic {
	Call  UseSettingsFrom   ( .Cam:Default 0` 0` 0` )
	Call  SetCamPitch       ( .Cam:Default *Fixed[15.000000] *Fixed[-6.000000] )
	Call  SetCamDistance    ( .Cam:Default 450` )
	Call  SetPanTarget      ( .Cam:Default *MapVar[A] *MapVar[B] *MapVar[C] )
	Call  SetCamSpeed       ( .Cam:Default *Fixed[1.000000] )
	Call  PanToTarget       ( .Cam:Default 00000000 00000001 )
	Call  WaitForCam        ( .Cam:Default *Fixed[1.0] )
	Return
	End
}

#new:Script $Script_SetResetPos_SignPlatform {
	Set *MapVar[9] 0`
	Return
	End
}

#new:Script $Script_SetResetPos_Dynamic {
	Call GetPlayerPos ( *VarA *VarB *VarC )
	Set *MapVar[9] 1`
	% respawn pos
	Set *MapVar[A] *VarA
	Set *MapVar[B] *VarB
	Set *MapVar[C] *VarC
	Return
	End
}

#new:Script $Script_ResetMario1 {
	Call DisablePartnerAI ( .True )
	Call DisablePlayerInput ( .True )
	
	Call SetPlayerAnimation ( 00080012 )
	
	If *MapVar[9] == 1`
		ExecWait $Script_PanCamera_Dynamic
	Else
		ExecWait $Script_PanCamera_PitCam2
	EndIf
	
	Call InterpPlayerYaw ( -207` 5` )
	
	If *MapVar[9] == 1`
		Set *VarB *MapVar[B]
		Add *VarB 5
		Set *VarC *MapVar[C]
		Add *VarC 2
		Call PlayEffect ( ~FX:Sparkles:Star *MapVar[A] *VarB *VarC 25` 0 0 0 0 0 0 0 0 )
	Else
		Call PlayEffect ( ~FX:Sparkles:Star ~Vec3D:PitCamFX 25` 0 0 0 0 0 0 0 0 )
	EndIf
	Call PlaySound ( 00000064 )
	
	Wait 3`
	
	Call SetPlayerAnimation ( 00010002 )
	
	If *MapVar[9] == 1`
		Call SetPlayerPos ( *MapVar[A] *MapVar[B] *MapVar[C] )
	Else
		Call SetPlayerPos ( ~Vec3D:PitCam2 )
	EndIf
	
	Call ResetCam ( .Cam:Default *Fixed[2.0] )
	
	Wait 10`
	
	Call EnablePartnerAI ()
	Call DisablePlayerInput ( .False )
	Return
	End
}

% Platform movement
#new:Script $Script_SetupPlatforms {
	Set *Var2 0`
	Set *Var3 -300`
	Set *Var6 140`
	Set *Var7 135`
	Set *Var8 ~Model:platform1_floor
	Set *Var9 ~Model:platform1
	Set *VarA ~Collider:platform1
	Exec $Script_MovePlatform
	
	Set *Var2 0`
	Set *Var3 215`
	Set *Var6 100`
	Set *Var7 95`
	Set *Var8 ~Model:platform3_floor
	Set *Var9 ~Model:platform3
	Set *VarA ~Collider:platform3
	Exec $Script_MovePlatform
	
	Wait 195`
	
	Set *Var2 0`
	Set *Var3 145`
	Set *Var6 100`
	Set *Var7 95`
	Set *Var8 ~Model:platform5_floor
	Set *Var9 ~Model:platform5
	Set *VarA ~Collider:platform5
	Exec $Script_MovePlatform
	Return
	End
}

% Script Arguments:
% Var2, Var3 - Start and End Z position
% Var6, Var7 - Time movement in one direction takes, resting time (in frames)
% Var8, Var9, VarA - Platform floor model, Platform group, Platform collider
#new:Script $Script_MovePlatform {
	Set *Var4 0`
	
	Call ParentColliderToModel ( *VarA *Var8 )
	Loop
		% 4.5 seconds at 30 fps = 135 frames
		Call MakeLerp ( *Var2 *Var3 *Var6 .Easing:Linear )
		Loop
			Call UpdateLerp ( )
			Call TranslateGroup ( *Var9 00000000 00000000 *Var0 )
			Call UpdateColliderTransform ( *VarA )
			% *Var5 -- delta Z
			Set *Var5 *Var0
			Sub *Var5 *Var4
			Set *Var4 *Var0
			Call $Function_AddPushZVelocity ( *Var5 *VarA ~Collider:unreachable )
			Wait 1`
			If *Var1 == 00000000
				BreakLoop
			EndIf
		EndLoop
		Wait *Var7
		Call MakeLerp ( *Var3 *Var2 *Var6 .Easing:Linear )
		Loop
			Call UpdateLerp ( )
			Call TranslateGroup ( *Var9 00000000 00000000 *Var0 )
			Call UpdateColliderTransform ( *VarA )
			% *Var5 -- delta Z
			Set *Var5 *Var0
			Sub *Var5 *Var4
			Set *Var4 *Var0
			Call $Function_AddPushZVelocity ( *Var5 *VarA ~Collider:unreachable )
			Wait 1`
			If *Var1 == 00000000
				BreakLoop
			EndIf
		EndLoop
		Wait *Var7
	EndLoop
	Return
	End
}

% Letter Delivery
#new:Unknown $LetterList_NpcGuard
{
	.Item:Letter06 00000000
}

#new:Script $Script_LetterDelivery_NpcGuard
{
	% the game would have used
	%
	%     Call  $Function_LetterDelivery_Init    ( ... )
	%
	% here, but that is equivalent to...
	
	Set *Var2 .NpcID:NpcGuard
	Set *Var3 00390205 % Talk Animation - same format as SpeakToPlayer
	Set *Var4 00390202 % Idle Animation - same format as SpeakToPlayer
	Set *Var5 .Item:Letter06 % same as LetterList
	Set *Var6 00000000 % same as LetterList
	Set *Var7 002F0055 % string by parakarry
	Set *Var8 002F0056 % string by parakarry when canceling the letter select box
	Set *Var9 002F0057 % "another letter duly delivered"
	Set *VarA 002F0058 % string by other npc
	Set *VarB $LetterList_NpcGuard
	
	ExecWait  $Script_DoLetterDelivery
	Return
	End
}

#new:Script $Script_LetterReward_NpcGuard
{
	% Script_DoLetterDelivery writes its return value into VarC
	% it's 0 if you don't have the letter
	% 1 if you do but cancel out of the item select box
	% and 2 if the letter was delivered
	
	If  *VarC  ==  00000002
		Call DisablePlayerInput ( .False )
		Call EnablePartnerAI ()
		
		% Flag 100 = Ignore Player Collision
		% If mode is true, the flag will be added to the npc
		% rather than removed, which is the case when it is false
		Call SetNpcFlagBits ( .NpcID:NpcGuard 00000100 .True )
		
		Call NpcMoveTo ( .NpcID:NpcGuard ~Vec2D:NpcGuard_Flee_Pos1 30` )
		Call NpcMoveTo ( .NpcID:NpcGuard ~Vec2D:NpcGuard_Flee_Pos2 30` )
		Call SetNpcPos ( .NpcID:NpcGuard 0 -100` 0 )
		
		Call ModifyColliderFlags ( 0 ~Collider:switch_room_blocker 00018000 )
		
		Set *MF_btf_06_ClubbaGone .True
	EndIf
	Return
	End
}

% Custom NPCs
#new:NpcGroup $NpcGroup_NpcGuard
{
	.NpcID:NpcGuard $NpcSettings_NpcGuard ~Vec3f:NpcGuard
	00402D09 $Script_Init_NpcGuard 0 0 0
	~NoDrops
	~Movement:NpcGuard
	~AnimationTable:NpcGuard
	00000000 00000000 00000000 00000000 % no tattle string
}

#new:NpcSettings $NpcSettings_NpcGuard
{
	00000000 001E0018 00000000 00000000 00000000 00000000 00000000 00000000
	00000000 00000000 00630000
}

#new:Script $Script_Init_NpcGuard
{
	If *MF_btf_06_ClubbaGone == .True
		Call SetNpcPos ( .Npc:Self ~Vec3D:BelowMap )
	Else
		Call BindNpcInteract ( .Npc:Self $Script_Interact_NpcGuard )
	EndIf
	
	Return
	End
}

#new:Script $Script_Interact_NpcGuard
{
	% talkAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 05
	% idleAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 02
	% stringID - AAAABBBB - space jam strings resource (0x2F), string 0x28
	If *MF_btf_19_LetterObtained == .True
		ExecWait  $Script_LetterDelivery_NpcGuard
		ExecWait  $Script_LetterReward_NpcGuard
		Return
	EndIf
	
	If *MF_btf_06_PlatformsActivated == .True
		If *MF_btf_06_PlatformMessageShown == .True
			% Now go play in the ball pit or whatever there is to see there.
			Call SpeakToPlayer ( .NpcID:NpcGuard 00390205 00390202 00000000 002F0028 )
		Else
			% Wait, you actually have a letter? ...
			Call SpeakToPlayer ( .NpcID:NpcGuard 00390205 00390202 00000000 002F0030 )
		EndIf
		Return
	EndIf
	
	If *MF_btf_06_PlatformMessageShown == .True
		% You desparately want to go over there? ...
		Call SpeakToPlayer ( .NpcID:NpcGuard 00390205 00390202 00000000 002F0027 )
		Call DisablePlayerInput ( .True )
		
		ExecWait $Script_PanCamera_AAAAAAAAAAAAAD
		
		Set *MF_btf_06_PlatformsActivated .True
		Exec $Script_SetupPlatforms
		
		Wait 60`
		
		Call ResetCam ( .Cam:Default *Fixed[4.0] )
		
		Call DisablePlayerInput ( .False )
		Call EndSpeech ( .NpcID:NpcGuard 00390205 00390202 00000000 )
		Return
	EndIf
	
	Call GetPlayerPos ( *VarA *VarB *VarC )
	Call ShowEmote ( 00000000 .Emote:Question -45` 30` .False *VarA *VarB *VarC 0` )
	Wait 60`
	% You want to know what's behind this door? TOO BAD! ...
	Call SpeakToPlayer ( .NpcID:NpcGuard 00390205 00390202 00000000 002F0025 )
	Call DisablePlayerInput ( .True )
	
	ExecWait $Script_PanCamera_AAAAAAAAAAAAAD
	
	Set *MF_btf_06_PlatformsActivated .True
	Exec $Script_SetupPlatforms
	
	Wait 60`
	
	Call ResetCam ( .Cam:Default *Fixed[4.0] )
	
	Call DisablePlayerInput ( .False )
	Wait 30`
	% Here you go, have fun.
	Call SpeakToPlayer ( .NpcID:NpcGuard 00390205 00390202 00000000 002F002F )
	
	Return
	End
}

#new:Script $Script_Platform_Fall
{
	If *MF_btf_06_PlatformMessageShown == .False
		If *MF_btf_06_PlatformsActivated == .False
			Call DisablePlayerInput ( .True )
			Call DisablePartnerAI ( .True )
			
			Call SpeakToPlayer ( .NpcID:NpcGuard 00390205 00390202 00000000 002F0026 )
			
			Call EnablePartnerAI ()
			Call DisablePlayerInput ( .False )
			Set *MF_btf_06_PlatformMessageShown .True
		EndIf
	EndIf
	Return
	End
}

#new:NpcGroup $NpcGroup_NpcFuzzy1
{
	.NpcID:NpcFuzzy1 $NpcSettings_8024144C ~Vec3f:NpcFuzzy1 % -388 100 -107
	00400400 $Script_Init_NpcFuzzy1 00000000 00000000 00000000
	~NoItems ~HP:Standard:2 ~FP:SlightlyReduced:2 ~CoinBonus:0:1
	~Movement:NpcFuzzy1
	~AnimationTable:NpcFuzzy1 % .Sprite:Fuzzy
	00000001 00000000 00000000 00000000 % no tattle string
}

#new:NpcGroup $NpcGroup_NpcFuzzy2
{
	.NpcID:NpcFuzzy2 $NpcSettings_8024144C ~Vec3f:NpcFuzzy2 % -388 100 -107
	00400400 $Script_Init_NpcFuzzy2 00000000 00000000 00000000
	~NoItems ~HP:Standard:2 ~FP:SlightlyReduced:2 ~CoinBonus:0:1
	~Movement:NpcFuzzy2
	~AnimationTable:NpcFuzzy2 % .Sprite:Fuzzy
	00000001 00000000 00000000 00000000 % no tattle string
}

#new:NpcSettings $NpcSettings_8024144C
{
	00000000 00140016 00000000 00000000 00000000 80077F70 00000000 8007809C
	00000000 00000000 00060000
}

#new:Script $Script_Init_NpcFuzzy1
{
	If *MF_btf_06_DefeatedNpcFuzzy1 == .True
		Call SetNpcPos ( .NpcID:NpcFuzzy1 ~Vec3D:BelowMap )
	Else
		Call BindNpcIdle       ( .NpcID:NpcFuzzy1 $Script_Idle_NpcFuzzy1 )
		Call BindNpcDefeat ( .NpcID:NpcFuzzy1 $Script_Defeat_NpcFuzzy1 )
	EndIf
	Return
	End
}

#new:Script $Script_Idle_NpcFuzzy1 {
	Call SetSelfEnemyFlagBits  ( 40000000 00000001 )
	Call BindNpcAI ( .NpcID:NpcFuzzy1 $Script_NpcAI_Fuzzy )
	
	Return
	End
}

#new:Script $Script_Defeat_NpcFuzzy1 {
	Set *MF_btf_06_DefeatedNpcFuzzy1 .True
	Return
	End
}

#new:Script $Script_Init_NpcFuzzy2
{
	If *MF_btf_06_DefeatedNpcFuzzy2 == .True
		Call SetNpcPos ( .NpcID:NpcFuzzy2 ~Vec3D:BelowMap )
	Else
		Call BindNpcIdle       ( .NpcID:NpcFuzzy2 $Script_Idle_NpcFuzzy2 )
		Call BindNpcDefeat ( .NpcID:NpcFuzzy2 $Script_Defeat_NpcFuzzy2 )
	EndIf
	Return
	End
}

#new:Script $Script_Idle_NpcFuzzy2 {
	Call SetSelfEnemyFlagBits  ( 40000000 00000001 )
	Call BindNpcAI ( .NpcID:NpcFuzzy2 $Script_NpcAI_Fuzzy )
	
	Return
	End
}

#new:Script $Script_Defeat_NpcFuzzy2 {
	Set *MF_btf_06_DefeatedNpcFuzzy2 .True
	Return
	End
}

#new:Script $Script_NpcAI_Fuzzy {
	Call  SetNpcFlagBits    ( .Npc:Self 00000040 .True )
	Call  $Function_80240BF0_nok_03 ( $AISettings_802413E0_nok_03 00000000 )
	Return
	End
}

#new:NpcGroup $NpcGroup_NpcBobomb1
{
	.NpcID:NpcBobomb1 $NpcSettings_NpcBobOmb1 ~Vec3f:BelowMap % spawned below map to basically unload it
	00002C00 $Script_Init_NpcBobomb1 00000000 00000000 000000B4
	~NoItems ~HP:Standard:2 ~FP:Standard:2 ~CoinBonus:0:1
	~Movement:NpcBobomb1
	~AnimationTable:NpcBobomb1 % .Sprite:Bombomb
	00000001 00000000 00000000 00000000 % no tattle string
}

#new:NpcSettings $NpcSettings_NpcBobOmb1
{
	00000000 00170014 00000000 00000000 00000000 80077F70 00000000 8007809C
	00000000 00000000 00060000
}

#new:Script $Script_Init_NpcBobomb1 {
	If *MF_btf_06_DefeatedNpcBobomb1 == .False
		Call BindNpcIdle ( .NpcID:NpcBobomb1 $Script_Idle_NpcBobomb1 )
	EndIf
	
	Return
	End
}

#new:Script $Script_ManuallyInit_NpcBobomb1 {
	If *MF_btf_06_DefeatedNpcBobomb1 == .True
		Call SetNpcPos ( .NpcID:NpcBobomb1 ~Vec3D:BelowMap )
	Else
		Call SetNpcPos ( .NpcID:NpcBobomb1 ~Vec3D:NpcBobomb1 )
		Call BindNpcIdle ( .NpcID:NpcBobomb1 $Script_Idle_NpcBobomb1 )
		Call BindNpcDefeat ( .NpcID:NpcBobomb1 $Script_Defeat_NpcBobomb1 )
	EndIf
	
	Return
	End
}

#new:Script $Script_Idle_NpcBobomb1 {
	Call BindNpcAI ( .NpcID:NpcBobomb1 $Script_NpcAI_NpcBobomb1 )
	Return
	End
}

#new:Script $Script_Defeat_NpcBobomb1 {
	Set *MF_btf_06_DefeatedNpcBobomb1 .True
	Return
	End
}

#new:Script $Script_NpcAI_NpcBobomb1 {
	% Guard AI
	Call  $Function_802413B8_trd_01 ( $AISettings_802434BC_trd_01 )
	Return
	End
}

#new:NpcGroup $NpcGroup_NpcKoopa1
{
	.NpcID:NpcKoopa1 $NpcSettings_NpcKoopa1 ~Vec3f:BelowMap
	00000C00 $Script_Init_NpcKoopa1 00000000 00000000 0000010E
	~NoItems ~HP:Standard:2 ~FP:Standard:2 ~CoinBonus:1:1
	~Movement:NpcKoopa1
	~AnimationTable:NpcKoopa1 % .Sprite:ParaTroopa
	00000000 00000000 00000000 00000000 % no tattle string
}

#new:NpcSettings $NpcSettings_NpcKoopa1 {
	00000000 00280018 00000000 00000000 00000000 80077F70 00000000 8007809C 
	00000000 00000000 00060001 
}

#new:Script $Script_Init_NpcKoopa1 {
	If *MF_btf_06_DefeatedNpcKoopa1 == .False
		Call BindNpcIdle ( .NpcID:NpcKoopa1 $Script_Idle_NpcKoopa1 )
	EndIf
	
	Return
	End
}

#new:Script $Script_ManuallyInit_NpcKoopa1 {
	If *MF_btf_06_DefeatedNpcKoopa1 == .True
		Call SetNpcPos ( .NpcID:NpcKoopa1 ~Vec3D:BelowMap )
	Else
		Call SetNpcPos ( .NpcID:NpcKoopa1 ~Vec3D:NpcKoopa1 )
		Call BindNpcDefeat ( .NpcID:NpcKoopa1 $Script_Defeat_NpcKoopa1 )
	EndIf
	
	Return
	End
}

#new:Script $Script_Idle_NpcKoopa1 {
	Call BindNpcAI ( .NpcID:NpcKoopa1 $Script_NpcAI_NpcKoopa1 )
	Return
	End
}

#new:Script $Script_Defeat_NpcKoopa1 {
	Set *MF_btf_06_DefeatedNpcKoopa1 .True
	Return
	End
}

#new:Script $Script_NpcAI_NpcKoopa1 {
	Call  SetSelfVar    ( 00000000 00000000 )
	Call  SetSelfVar    ( 00000005 FFFFFD76 )
	Call  SetSelfVar    ( 00000006 0000001E )
	Call  SetSelfVar    ( 00000001 00000258 )
	Call  $Function_802421CC_nok_14 ( $AISettings_80243A30_nok_14 )
	Return
	End
}

#new:NpcGroup $NpcGroup_DiamondHelper
{
	.NpcID:DiamondHelper $NpcSettings_DiamondHelper ~Vec3f:BelowMap
	00442D09 $Script_Init_DiamondHelper 0 0 0
	~NoDrops
	~Movement:DiamondHelper
	~AnimationTable:DiamondHelper
	00000000 00000000 00000000 00000000 % no tattle string
}

#new:NpcSettings $NpcSettings_DiamondHelper
{
	00000000 001E0018 00000000 00000000 00000000 00000000 00000000 00000000
	00000000 00000000 00630000
}

#new:Script $Script_Init_DiamondHelper
{
	Call BindNpcIdle ( .Npc:Self $Script_Idle_DiamondHelper )
	Return
	End
}

#new:Script $Script_Idle_DiamondHelper {
	If *MF_btf_06_CollectedDiamondStone  ==  .False
		Label  0
		Wait  1`
		If  *MF_btf_06_CollectedDiamondStone  ==  .False
			Goto  0
		EndIf
		
		% Idle Script pauses during Item get dialogue
		% but 1 frame later than it would in vanilla maps??
		Wait 1`
		
		% when the dialogue closes and this script resumes, that's when the cutscene is shown
		Exec $Script_ShowDiamondCutscene
	EndIf
	
	If *MF_btf_06_CollectedLunarStone  ==  .False
		Label  1
		Wait  1`
		If  *MF_btf_06_CollectedLunarStone  ==  .False
			Goto  1
		EndIf
		
		Wait 1`
		
		Exec $Script_ShowLunarCutscene
	EndIf
	
	Return
	End
}

#new:NpcGroupList $NpcGroupList {
	00000001 $NpcGroup_NpcGuard      00000000
	00000001 $NpcGroup_NpcFuzzy1     05190000
	00000001 $NpcGroup_NpcFuzzy2     051A0000
	00000001 $NpcGroup_NpcBobomb1    051B0000
	00000001 $NpcGroup_NpcKoopa1     051C0000
	00000001 $NpcGroup_DiamondHelper 00000000
	00000000 00000000 00000000
}
