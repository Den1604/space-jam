#define .NpcID:NpcGuard 1`
#define .NpcID:NpcSpringDummy 2`

#new:Script $Script_OpenChestAnimation
{
    0:  SetGroup  00000000
    C:  Call  SetTimeFreezeMode ( 00000002 )
   1C:  Wait  40`
   28:  Call  ShowGotItem       ( *Var0 .False 00000000 )
   40:  Call  SetTimeFreezeMode ( 00000000 )
   50:  Return
   58:  Return
   60:  End
}

#new:Script $Script_OpenChest
{
    0:  Call  DisablePlayerInput    ( .True )
   10:  Set   *Var0  *VarA
   20:  If  *VarA  !=  00000000
   30:  	ExecWait  $Script_OpenChestAnimation
   3C:  EndIf
   44:  Switch  *VarB
   50:  	Case  ==  00000000
   5C:  		Call  AddItem   ( *VarA *Var0 )
   70:  	Case  ==  00000001
   7C:  		Call  AddKeyItem    ( *VarA )
   8C:  	Case  ==  00000002
   98:  		Call  AddBadge  ( *VarA *Var0 )
   AC:  EndSwitch
   B4:  Wait  15`
   C0:  Call  DisablePlayerInput    ( .False )
   D0:  Return
   D8:  End
}

% pasted in here because SR generates 20,000000 instead of 20.000000
% causing a compilation error :)
@ $Script_PanCamera_AAAAAAAAAAAAAD
{
	Call  SetCamType        ( .Cam:Default 00000001 .False )
	Call  SetCamPitch       ( .Cam:Default *Fixed[20.000000] *Fixed[-7.000000] )
	Call  SetCamDistance    ( .Cam:Default 650` )
	Call  SetCamPosA        ( .Cam:Default 23` 10` )
	Call  SetCamPosB        ( .Cam:Default 22` 0` )
	Call  SetCamPosC        ( .Cam:Default 10` 60` )
	Call  SetPanTarget      ( .Cam:Default ~Vec3d:AAAAAAAAAAAAAD )
	Call  SetCamSpeed       ( .Cam:Default *Fixed[2.500000] )
	Call  PanToTarget       ( .Cam:Default 00000000 00000001 )
	Call  WaitForCam        ( .Cam:Default *Fixed[1.0] )
	Return
	End
}

@ $Script_Main {
	Set   *GB_WorldLocation  .Location:GoombaRoad
	Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
	Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
	Call  SetCamEnabled      ( .Cam:Default .True
	Call  SetCamLeadPlayer   ( .Cam:Default .False )
	
	% Generated
	Exec  $Script_MakeEntities
	Exec  $Script_SetupMusic
	Exec  $Script_EnterMap
	% Custom
	Exec  $Script_MakeItems
	Exec  $Script_SetupItemCutscenes
	Exec  $Script_MakeCustomEntities
	
	If *MF_btf_06_PlatformsActivated == .True
		Exec  $Script_SetupPlatforms
	EndIf
	
	Call MakeNpcs ( .False $NpcGroupList )
	Call SetNpcYaw ( .NpcID:NpcGuard -127` ) % Makes Clubba face left
	Bind $Script_Platform_Fall .Trigger:FloorTouch ~Collider:platform1 1 00000000
	
	Return
	End
}

#new:Script $Script_MakeItems {
	Call MakeItemEntity ( .Item:TastyTonic ~Vec3d:Item1 .ItemSpawnMode:Fixed_NeverVanish *MF_btf_06_CollectedItem1 )
	Call MakeItemEntity ( .Item:DiamondStone ~Vec3d:ItemDiamondStone .ItemSpawnMode:Fixed_NeverVanish *MF_btf_06_CollectedDiamondStone )
	Return
	End
}

#new:Script $Script_SetupItemCutscenes {
	Call AwaitPlayerApproach ( ~Vec2D:ItemDiamondStone 19` )
	Call AwaitPlayerLeave ( ~Vec2D:ItemDiamondStone 19` )
	
	Call DisablePlayerInput ( .True )
	
	Wait 30`
	
	% ExecWait $Script_PanCamera_AAAAAAAAAAAAAD
	
	ExecWait $Script_PanCamera_SpringCam
	
	Wait 30`
	
	% Call SetNpcPos ( .NpcID:NpcSpringDummy ~Vec3D:Spring )
	
	% talkAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 05
	% idleAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 02
	% stringID - AAAABBBB - space jam strings resource (0x2F), string 0x28
	% Call SpeakToPlayer ( .Npc:Partner 00040006 00040001 00000000 002F002F )
	
	Return
	End
}

@Message $String_ReadSign_Signpost1
{
	[DelayOff][STYLE:SIGN]
	[CenterX:FF]
	[Down:03]Only the quickest heroes may[BR]dare to ride the platforms.
	[DelayOn][WAIT][END]
}

% Custom entities
#new:Script $Script_MakeCustomEntities {
	Call  MakeEntity    ( .Entity:Chest ~Vec4d:Chest1 00000000 80000000 )
	Call  AssignFlag    ( *MF_btf_06_OpenedChest1 )
	Call  AssignScript  ( $Script_UseChest1 )
	
	Call  MakeEntity    ( .Entity:Chest ~Vec4d:Chest2 00000000 80000000 )
	Call  AssignFlag    ( *MF_btf_06_OpenedChest2 )
	Call  AssignScript  ( $Script_UseChest2 )
	
	Call  MakeEntity    ( .Entity:YellowBlock ~Vec4d:ItemBlock1 .Item:DizzyDial 80000000 )
	Call  AssignBlockFlag   ( *MF_btf_06_HitItemBlock1 )
	
	Return
	End
}

#new:Script $Script_UseChest1
{
	Set *VarA .Item:JumpCharge
	Set *VarB 00000002 % 2 for badge
	Set *MF_btf_06_OpenedChest1 .True
	ExecWait $Script_OpenChest
	Return
	End
}

#new:Script $Script_UseChest2
{
	Set *VarA .Item:AttackFXA
	Set *VarB 00000002 % 2 for badge
	Set *MF_btf_06_OpenedChest2 .True
	ExecWait $Script_OpenChest
	Return
	End
}


% Platform movement
#new:Script $Script_SetupPlatforms {
	Set *MF_btf_06_PlatformsActivated .True
	
	Set *Var2 0`
	Set *Var3 -300`
	Set *Var6 140`
	Set *Var7 135`
	Set *Var8 ~Model:platform1_floor
	Set *Var9 ~Model:platform1
	Set *VarA ~Collider:platform1
	Exec $Script_MovePlatform
	
	Set *Var2 0`
	Set *Var3 215`
	Set *Var6 100`
	Set *Var7 95`
	Set *Var8 ~Model:platform3_floor
	Set *Var9 ~Model:platform3
	Set *VarA ~Collider:platform3
	Exec $Script_MovePlatform
	
	Wait 195`
	
	Set *Var2 0`
	Set *Var3 145`
	Set *Var6 100`
	Set *Var7 95`
	Set *Var8 ~Model:platform5_floor
	Set *Var9 ~Model:platform5
	Set *VarA ~Collider:platform5
	Exec $Script_MovePlatform
	Return
	End
}

% Script Arguments:
% Var2, Var3 - Start and End Z position
% Var6, Var7 - Time movement in one direction takes, resting time (in frames)
% Var8, Var9, VarA - Platform floor model, Platform group, Platform collider
#new:Script $Script_MovePlatform {
	Set *Var4 0`
	
	Call ParentColliderToModel ( *VarA *Var8 )
	Loop
		% 4.5 seconds at 30 fps = 135 frames
		Call MakeLerp ( *Var2 *Var3 *Var6 .Easing:Linear )
		Loop
			Call UpdateLerp ( )
			Call TranslateGroup ( *Var9 00000000 00000000 *Var0 )
			Call UpdateColliderTransform ( *VarA )
			% *Var5 -- delta Z
			Set *Var5 *Var0
			Sub *Var5 *Var4
			Set *Var4 *Var0
			Call $Function_AddPushZVelocity ( *Var5 *VarA ~Collider:unreachable )
			Wait 1`
			If *Var1 == 00000000
				BreakLoop
			EndIf
		EndLoop
		Wait *Var7
		Call MakeLerp ( *Var3 *Var2 *Var6 .Easing:Linear )
		Loop
			Call UpdateLerp ( )
			Call TranslateGroup ( *Var9 00000000 00000000 *Var0 )
			Call UpdateColliderTransform ( *VarA )
			% *Var5 -- delta Z
			Set *Var5 *Var0
			Sub *Var5 *Var4
			Set *Var4 *Var0
			Call $Function_AddPushZVelocity ( *Var5 *VarA ~Collider:unreachable )
			Wait 1`
			If *Var1 == 00000000
				BreakLoop
			EndIf
		EndLoop
		Wait *Var7
	EndLoop
	Return
	End
}

% Custom NPCs
#new:NpcGroup $NpcGroup_NpcGuard
{
	.NpcID:NpcGuard $NpcSettings_NpcGuard ~Vec3f:NpcGuard
	00402D09 $Script_Init_NpcGuard 0 0 0
	~NoDrops
	~Movement:NpcGuard
	~AnimationTable:NpcGuard
	00000000 00000000 00000000 00000000 % no tattle string
}

#new:NpcSettings $NpcSettings_NpcGuard
{
	00000000 001E0018 00000000 00000000 00000000 00000000 00000000 00000000
	00000000 00000000 00630000
}

#new:Script $Script_Init_NpcGuard
{
	Call BindNpcInteract ( .Npc:Self $Script_Interact_NpcGuard )
    Return
    End
}

#new:NpcGroup $NpcGroup_NpcSpringDummy
{
	.NpcID:NpcSpringDummy $NpcSettings_NpcSpringDummy ~Vec3f:NpcSpringDummy
	00402D09 $Script_Init_NpcSpringDummy 0 0 0
	~NoDrops
	~Movement:NpcSpringDummy
	~AnimationTable:NpcSpringDummy
	00000000 00000000 00000000 00000000 % no tattle string
}

#new:NpcSettings $NpcSettings_NpcSpringDummy
{
	00000000 001E0018 00000000 00000000 00000000 00000000 00000000 00000000
	00000000 00000000 00630000
}

#new:Script $Script_Init_NpcSpringDummy
{
    Return
    End
}

#new:Script $Script_Interact_NpcGuard
{
	% talkAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 05
	% idleAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 02
	% stringID - AAAABBBB - space jam strings resource (0x2F), string 0x28
	If *MF_btf_06_PlatformsActivated == .True
		If *MF_btf_06_PlatformMessageShown == .True
			% Now go play in the ball pit or whatever there is to see there.
			Call SpeakToPlayer ( .NpcID:NpcGuard 00390105 00390102 00000000 002F0028 )
		Else
			% Wait, you actually have a letter? ...
			Call SpeakToPlayer ( .NpcID:NpcGuard 00390105 00390102 00000000 002F0030 )
		EndIf
		% TODO: remove, this is temporary
		Set *MF_btf_06_PlatformsActivated .False
		Set *MF_btf_06_PlatformMessageShown .False
		Return
	EndIf
	
	If *MF_btf_06_PlatformMessageShown == .True
		% You desparately want to go over there? ...
		Call SpeakToPlayer ( .NpcID:NpcGuard 00390105 00390102 00000000 002F0027 )
		Call DisablePlayerInput ( .True )
		ExecWait $Script_PanCamera_AAAAAAAAAAAAAD
		Exec $Script_SetupPlatforms
		Wait 60`
		Call ResetCam ( .Cam:Default *Fixed[4.0] )
		Call DisablePlayerInput ( .False )
		Call EndSpeech ( .NpcID:NpcGuard 00390105 00390102 00000000 )
		Return
	EndIf
	
    Call GetPlayerPos ( *VarA *VarB *VarC )
    Call ShowEmote ( 00000000 .Emote:Question -45` 30` .False *VarA *VarB *VarC 0` )
	Wait 60`
	% TODO: some text is too small to be readable with N64 or LLE grapics, test a better font size with ares
	% You want to know what's behind this door? TOO BAD! ...
	Call SpeakToPlayer ( .NpcID:NpcGuard 00390105 00390102 00000000 002F0025 )
	Call DisablePlayerInput ( .True )
	ExecWait $Script_PanCamera_AAAAAAAAAAAAAD
	Exec $Script_SetupPlatforms
	Wait 60`
	Call ResetCam ( .Cam:Default *Fixed[4.0] )
	Call DisablePlayerInput ( .False )
	Wait 30`
	% Here you go, have fun.
	Call SpeakToPlayer ( .NpcID:NpcGuard 00390105 00390102 00000000 002F002F )
	
	Return
	End
}

#new:Script $Script_Platform_Fall
{
	% Wait 60`
	
	If *MF_btf_06_PlatformMessageShown == .False
		If *MF_btf_06_PlatformsActivated == .False
			Call DisablePlayerInput ( .True )
			Call SpeakToPlayer ( .NpcID:NpcGuard 00390105 00390102 00000000 002F0026 )
			Call DisablePlayerInput ( .False )
			Set *MF_btf_06_PlatformMessageShown .True
		EndIf
	EndIf
	Return
	End
}

#new:NpcGroupList $NpcGroupList {
	00000001 $NpcGroup_NpcGuard 00000000
	00000001 $NpcGroup_NpcSpringDummy 00000000
	00000000 00000000 00000000
}
