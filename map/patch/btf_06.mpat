#new:Function $Function_AddPushVelocityZ
{
    0:  ADDIU     SP, SP, FFE0
    4:  SW        S1, 14 (SP)
    8:  COPY      S1, A0
    C:  SW        RA, 1C (SP)
   10:  SW        S2, 18 (SP)
   14:  SW        S0, 10 (SP)
   18:  LW        S0, C (S1)
   1C:  LW        A1, 0 (S0)
   20:  JAL       ~Func:get_variable
   24:  ADDIU     S0, S0, 4
   28:  LW        A1, 0 (S0)
   2C:  ADDIU     S0, S0, 4
   30:  COPY      A0, S1
   34:  JAL       ~Func:get_variable
   38:  COPY      S2, V0
   3C:  COPY      A0, S1
   40:  LW        A1, 0 (S0)
   44:  JAL       ~Func:get_variable
   % S1 -- floorA
   % S0 -- floorB
   48:  COPY      S1, V0
   4C:  COPY      S0, V0
   % V0 -- collisionStatus
   % V1 -- collisionStatus->curFloor
   50:  LA        V0, 8015A550
   58:  LH        V1, 2 (V0)
   5C:  BEQ       V1, S1, .o80 % if collisionStatus->curFloor == floorA -> .o80
   60:  NOP
   % V0 -- collisionStatus->lastTouchedFloor
   64:  LH        V0, 4 (V0)
   68:  BEQ       V0, S1, .o80 % if collisionStatus->lastTouchedFloor == floorA
   6C:  NOP
   70:  BEQ       V1, S0, .o80 % if collisionStatus->curFloor == floorB
   74:  NOP
   78:  BNE       V0, S0, .o98 % if collisionStatus->lastTouchedFloor == floorB -> just continue, else -> .o98
   7C:  NOP
        .o80
   % F0 -- velX as float
   80:  MTC1      S2, F0
   84:  NOP
   88:  CVT.S.W   F0, F0
   % V0 -- playerStatus pointer
   8C:  LA        V0, 8010EFC8
   % playerStatus->pushVel.x (offset 1C) = F0
   94:  SWC1      F0, 24 (V0) % modified, originally 1C for .x, now 24 for .z
        .o98
   % end of if statement
   98:  LAB       V0, 8010F2A2
   A0:  BEQ       V0, R0, .oDC % if 
   A4:  NOP
   A8:  JAL       ~Func:get_npc_unsafe
   AC:  LI        A0, FFFC
   % V0 -- partner npc pointer (return value from get_npc_unsafe)
   % V1 -- partner->curFloor
   B0:  LH        V1, 84 (V0)
   B4:  BEQ       V1, S1, .oC4 % if partner->curFloor == floorA
   B8:  NOP
   BC:  BNE       V1, S0, .oDC % if partner->curFloor == floorB then just continue, else -> .oDC
   C0:  NOP
        .oC4
   % F0 -- partner->pos.x
   % F2 -- velX as float
   C4:  LWC1      F0, 38 (V0)
   C8:  MTC1      S2, F2
   CC:  NOP
   D0:  CVT.S.W   F2, F2
   D4:  ADD.S     F0, F0, F2
   % partner->pos.z = partner->pos.x + velX
   D8:  SWC1      F0, 40 (V0) % modified, originally 38 for .x, now 40 for .z
        .oDC
   DC:  LW        RA, 1C (SP)
   E0:  LW        S2, 18 (SP)
   E4:  LW        S1, 14 (SP)
   E8:  LW        S0, 10 (SP)
   EC:  LI        V0, 2
   F0:  JR        RA
   F4:  ADDIU     SP, SP, 20
}

#define .NpcID:NpcGuard 1`

@ $Script_Main {
	Set   *GB_WorldLocation  .Location:GoombaRoad
	Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
	Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
	Call  SetCamEnabled      ( .Cam:Default .True
	Call  SetCamLeadPlayer   ( .Cam:Default .False )
	
	% Generated
	Exec  $Script_MakeEntities
	Exec  $Script_SetupMusic
	Exec  $Script_EnterMap
	% Custom
	Exec  $Script_MakeItems
	Exec  $Script_MovePlatform
	
	Call MakeNpcs ( .False $NpcGroupList )
	Call SetNpcYaw ( .NpcID:NpcGuard -127` ) % Makes Clubba face left
	Bind $Script_Platform_Fall .Trigger:FloorTouch ~Collider:platform1 1 00000000
	Return
	End
}

#new:Script $Script_MakeItems {
	Call MakeItemEntity ( .Item:TastyTonic ~Vec3d:Item1 .ItemSpawnMode:Fixed_NeverVanish *MF_btf_06_CollectedItem1 )
	Return
	End
}

% Platform movement
#new:Script $Script_MovePlatform {
	% Constants
	Set *Var2 0`
	Set *Var3 -300`
	% Variables
	Set *Var4 0`
	
	Call ParentColliderToModel ( ~Collider:platform1 ~Model:platform1_floor )
	Loop
		% 4.5 seconds at 30 fps = 135 frames
		Wait 135`
		Call MakeLerp ( *Var2 *Var3 140` .Easing:Linear )
		Loop
			Call UpdateLerp ( )
			Call TranslateGroup ( ~Model:platform1 00000000 00000000 *Var0 )
			Call UpdateColliderTransform ( ~Collider:platform1 )
			% *Var5 -- delta Z
			Set *Var5 *Var0
			Sub *Var5 *Var4
			Set *Var4 *Var0
			Call $Function_AddPushVelocityZ ( *Var5 ~Collider:platform1 ~Collider:unreachable )
			Wait 1`
			If *Var1 == 00000000
				BreakLoop
			EndIf
		EndLoop
		Wait 135`
		Call MakeLerp ( *Var3 *Var2 140` .Easing:Linear )
		Loop
			Call UpdateLerp ( )
			Call TranslateGroup ( ~Model:platform1 00000000 00000000 *Var0 )
			Call UpdateColliderTransform ( ~Collider:platform1 )
			% *Var5 -- delta Z
			Set *Var5 *Var0
			Sub *Var5 *Var4
			Set *Var4 *Var0
			Call $Function_AddPushVelocityZ ( *Var5 ~Collider:platform1 ~Collider:unreachable )
			Wait 1`
			If *Var1 == 00000000
				BreakLoop
			EndIf
		EndLoop
	EndLoop
	Return
	End
}

% Custom NPC
#new:NpcGroup $NpcGroup_NpcGuard
{
	.NpcID:NpcGuard $NpcSettings_NpcGuard ~Vec3f:NpcGuard
	00402D09 $Script_Init_NpcGuard 0 0 0
	~NoDrops
	~Movement:NpcGuard
	~AnimationTable:NpcGuard
	00000000 00000000 00000000 00000000 % no tattle string
}

#new:NpcSettings $NpcSettings_NpcGuard
{
	00000000 001E0018 00000000 00000000 00000000 00000000 00000000 00000000
	00000000 00000000 00630000
}

#new:Script $Script_Init_NpcGuard
{
	Call BindNpcInteract ( .Npc:Self $Script_Interact_NpcGuard )
    Return
    End
}

#new:Script $Script_Interact_NpcGuard
{
	% talkAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 05
	% idleAnim - AAAABBCC - overworld clubba (0x39), palette 01, animation 02
	Call SpeakToPlayer ( .NpcID:NpcGuard 00390105 00390102 00000000 00030029 )
	Return
	End
}

#new:Script $Script_Platform_Fall
{
	% Wait 60`
	
	If *MF_btf_06_PlatformMessageShown == .False
		Call DisablePlayerInput ( .True )
		Call SpeakToPlayer ( .NpcID:NpcGuard 00390105 00390102 00000000 00030029 )
		Call DisablePlayerInput ( .False )
		Set *MF_btf_06_PlatformMessageShown .True
	EndIf
	Return
	End
}

#new:NpcGroupList $NpcGroupList {
	00000001 $NpcGroup_NpcGuard 00000000
	00000000 00000000 00000000
}
