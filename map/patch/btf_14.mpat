@ $Script_SetupMusic {
    Call  SetMusicTrack ( 00000000 .Song:LotofDreams 00000000 00000008 )
    Call  ClearAmbientSounds ( 250` )
    Return
    End
}

@ $Script_Main {
    Set   *GB_WorldLocation  .Location:GoombaRoad
    Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
    Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
    Call  SetCamEnabled      ( .Cam:Default .True )
    Call  SetCamLeadPlayer   ( .Cam:Default .False )
    Exec  $Script_SetupMusic
    Exec  $Script_SetupTexturePan
    Exec  $Script_EnterMap
    Exec  $Script_MakeEntities
    Bind $Script_MarioAndParakarryLeave .Trigger:WallPressA ~Collider:LeaveCollider 00000001 00000000
    Return
    End
}

#new:Script $Script_MakeEntities
{
    Call  MakeEntity    ( .Entity:YellowBlock ~Vec4d:YellowBlock .Item:YoshiCookie 80000000 )
    Call  AssignBlockFlag   ( *MF_btf_14_YellowBlock )
    Call  MakeEntity    ( .Entity:HiddenRedBlock ~Vec4d:HiddenBlock .Item:DodgeMaster 80000000 )
    Call  AssignBlockFlag   ( *MF_btf_14_HiddenRedBlock )
    Call  MakeEntity    ( .Entity:HiddenRedBlock ~Vec4d:AttackFXE .Item:AttackFXE 80000000 )
    Call  AssignBlockFlag   ( *MF_btf_14_AttackFXE )
    Return
    End
}

@ $Script_EnterMap
{
	Call  GetEntryID    ( *Var[0] )
	Switch  *Var[0]
		Case  ==  ~Entry:Entry0
			Set   *Var[0] $Script_CreateExitTriggers
			Exec  EnterWalk
		Case  ==  ~Entry:Entry1
            Exec $Script_ArrivalCutscene
			Exec  $Script_CreateExitTriggers
		Default
			Exec  $Script_CreateExitTriggers
	EndSwitch
	Return
	End
}

#new:Script $Script_ArrivalCutscene
{
    Call DisablePlayerInput ( .True )
    Call DisablePartnerAI ( 00000000 )
    Call PlaySound ( 0000032A )
    Call TranslateGroup ( ~Model:SpaceTruckGroup -440` 200` 0` )
    Set *Var3 -451`
    Call  MakeLerp  ( 200` 0` 40` .Easing:Linear )
    Loop 
        Add *Var3 11`
        Call UpdateLerp ( )
        Call TranslateGroup ( ~Model:SpaceTruckGroup *Var3 *Var0 0` )
            If *Var1 == 0
                BreakLoop
            EndIf
        Wait 1`
    EndLoop
    Call SetPlayerPos ( ~Vec3D:MarioPos2 )
    Call SetNpcPos ( .Npc:Partner ~Vec3D:ParakarryPos )
    Call StopSound ( 0000032A )
    Set *Var0 0`
    Loop 9`
        Add *Var0 15`
        Call RotateModel ( ~Model:SpaceTruck_Roof *Var0 0` 0` 1` )
        Wait 1`
    EndLoop
    Call SetPlayerJumpscale ( *Fixed[2.0] )
    Call PlayerJump ( ~Vec3D:MarioJumpTo 20` )
    Call NpcFlyTo ( .Npc:Partner ~Vec3D:ParakarryFlyTo1 15` 1` .Easing:Linear )
    Call NpcFlyTo ( .Npc:Partner ~Vec3D:ParakarryFlyTo2 25` 1` .Easing:Linear )
    Wait 10`
    Loop 9`
        Sub *Var0 15`
        Call RotateModel ( ~Model:SpaceTruck_Roof *Var0 0` 0` 1` )
        Wait 1`
    EndLoop
    Wait 10`
    Call  SpeakToPlayer ( .Npc:Partner 00040006 00040001 00000000 002F002D )
    Call EnablePartnerAI ()
    Call DisablePlayerInput ( .False )
    Return
    End
}

#new:Script $Script_MarioAndParakarryLeave
{
    Call DisablePlayerInput ( .True )
    Call DisablePartnerAI ( 00000000 )
    If *MF_btf_16_GourmetGuyLetterDelivered == .True
        Call SpeakToPlayer ( .Npc:Partner 00040006 00040001 00000000 002F0038 )
        Call ShowChoice ( 001E0004 )
        Call CloseMessage ()
        If *Var0 == 0` %Yes
            Call SpeakToPlayer ( .Npc:Partner 00040006 00040001 00000000 002F0039 )
            Set *Var0 0`
            Loop 9`
                Add *Var0 15`
                Call RotateModel ( ~Model:SpaceTruck_Roof *Var0 0` 0` 1` )
                Wait 1`
            EndLoop
            Call EnablePartnerAI ()
            Call SetPlayerJumpscale ( *Fixed[2.0] )
            Call PlayerJump ( ~Vec3D:MarioPos2 20` )
            Call EnablePartnerAI ()
            Wait 10`
            Call DisablePartnerAI ( 00000000 )
            Call NpcFlyTo ( .Npc:Partner ~Vec3D:ParakarryFlyTo1 25` 1` .Easing:Linear )
            Call NpcFlyTo ( .Npc:Partner ~Vec3D:ParakarryPos 15` 1` .Easing:Linear )
            Call SetPlayerPos ( ~Vec3D:MarioPos )
            Call SetNpcPos ( .Npc:Partner ~Vec3D:MarioPos )
            Loop 9`
                Sub *Var0 15`
                Call RotateModel ( ~Model:SpaceTruck_Roof *Var0 0` 0` 1` )
                Wait 1`
            EndLoop
            Call PlaySound ( 0000032A )
            
            Set *Var2 0`
            Call MakeLerp ( 0` 600` 40` .Easing:QuadraticIn )
            Loop
                Call UpdateLerp ( )
                Call TranslateGroup ( ~Model:SpaceTruckGroup *Var0 *Var2 0` )
                Add *Var2 3`
                Wait 1`
                If *Var1 == 00000000
                    BreakLoop
                EndIf
            EndLoop
            
            Wait 10`
            Call GotoMap ( "btf_13" 00000002 )
            Call StopSound ( 0000032A )
            Wait 300`
        Else %No
            Call SpeakToPlayer ( .Npc:Partner 00040006 00040001 00000000 002F0024 )
        EndIf
    Else
        Call SpeakToPlayer ( .Npc:Partner 00040006 00040001 00000000 002F002E )
    EndIf
    Call EnablePartnerAI ()
    Call DisablePlayerInput ( .False )
    Return
    End
}